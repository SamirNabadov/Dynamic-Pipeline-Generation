plugins {
    id 'com.github.sherter.google-java-format' version '0.9' apply false
	id 'io.spring.dependency-management' version "$springDependencyManagerVersion" apply false
	id 'com.google.cloud.tools.jib' version "$jlibVersion" apply false
}

def jibUsingProjects = [
		project(':app'),
		project(':base')
]

allprojects {
    version = '1.0'
}

subprojects {
    apply plugin: 'java'
    apply plugin: 'com.github.sherter.google-java-format'

    dependencies {
        // for type inference
        annotationProcessor 'org.projectlombok:lombok:1.18.12'
        compileOnly 'org.projectlombok:lombok:1.18.12'
        testAnnotationProcessor 'org.projectlombok:lombok:1.18.12'
        testCompileOnly 'org.projectlombok:lombok:1.18.12'

        // for logging
        implementation 'ch.qos.logback:logback-classic:1.2.3'
        implementation 'org.slf4j:slf4j-api:1.7.26'

        implementation 'com.google.guava:guava:29.0-jre'
        testImplementation 'org.junit.jupiter:junit-jupiter-api:5.6.2'
        testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.6.2'
    }

    repositories {
        jcenter()
    }

    test {
        useJUnitPlatform()
    }

    compileJava.dependsOn tasks.googleJavaFormat
}

configure(jibUsingProjects) {
	apply plugin: 'com.google.cloud.tools.jib'

	jib {
		allowInsecureRegistries = true
		to {
			image = "$dockerRepoUrl/${project.name}"
		}
		container {
			creationTime = 'USE_CURRENT_TIMESTAMP'
		}
	}

}